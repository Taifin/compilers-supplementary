-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[st, w], stmt) {
  case stmt of
      Assn (x, e) -> [st <- [x, evalExpr(st, e)], w]
    | Seq (stmt1, stmt2) -> eval(eval(c, stmt1), stmt2)
    | Skip -> c
    | Read (x) -> 
        case readWorld(w) of
          [v, nw] -> [st <- [x, v], nw]
        esac
    | Write (e) -> [st, writeWorld(evalExpr(st, e), w)]
    | If (e, s1, s2) -> if evalExpr(st, e) then eval(c, s1) else eval(c, s2) fi
    | While (e, s) -> while (evalExpr(c[0], e)) do c := eval(c, s) od; c
    | DoWhile(s, e) -> do c := eval(c, s) while (evalExpr(c[0], e)) od; c
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
